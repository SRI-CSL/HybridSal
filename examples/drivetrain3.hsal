drivetrain3: CONTEXT = 
BEGIN

INV(w_engine:REAL, w_gear:REAL, w_load:REAL, tau: REAL, w_engine2:REAL, w_gear2:REAL, w_load2:REAL, tau2: REAL, throttleby502: REAL): BOOLEAN =
  throttleby502 >= 0 AND throttleby502 <= 40 AND
  w_engine >= 0 AND w_engine2 >= 0 AND
  w_gear >= 0 AND w_gear2 >= 0 AND
  w_load >= 0 AND w_load2 >= 0 AND
  tau >= 0 AND tau2 >= 0 ;

% f7 = w_engine = angular velocity engine (before gear)
% f17 = w_gear = angular velocity gear box (after gear?)
% f5 = w_load = angular velocity of the load (car)
% e11 = tau = torque generated by drive shaft (and seen by car/factor)
% drive_shaft_compliance = angle/Nm = angle_deformed/torsion applied

% I_Engine * dw_engine/dt = Throttle - w_engine*BearingLoss - (w_engine-w_gear)*ClutchResistance
% I_GearBox * d (w_gear)/dt =  (w_engine-w_gear)*ClutchResistance - tau*Ratio
% I_Load * d w_load/dt = tau/ParameterTF-w_load*LoadResistance
% DriveShaftCompliance * d tau/dt = w_gear*Ratio - w_load

% Throttle: REAL = 2000 ;		% throttle setting (input)
% BearingLoss: REAL = 1/100;
% ClutchResistance: REAL = 1000;
% I_Engine: REAL = 50;
% I_GearBox: REAL = 10;
% I_Load: REAL = 4000;
% ParameterTF: REAL = 6/10;
% LoadResistance: REAL = 200;	% RoadScenario (input)
% DriveShaftCompliance: REAL = 1/10000000;


% e11new = e11old / 10000
plant: MODULE =
BEGIN
OUTPUT f5, f7, e11, f17:REAL
LOCAL throttleby50: REAL
INPUT ratio: REAL
INITIALIZATION
  throttleby50 IN {z:REAL | 0 <= z AND z <= 40};
  f5 = 0; f7 = 0; e11 = 0; f17 = 0
TRANSITION
[
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1/4 AND (ratio' = 1/3 OR ratio' = 1) --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1/4 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/40
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1/3 AND (ratio' = 1/3 OR ratio' = 3/7) --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1/3 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/30
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 3/7 AND (ratio' = 1/3 OR ratio' = 3/5) --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 3/7 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 3/70
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 3/5 AND (ratio' = 3/7 OR ratio' = 1) --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 3/5 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 3/50
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1 AND (ratio' = 3/5 OR ratio' = 7/5) --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/10
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 7/5 AND ratio' = 1 --> 
  f5dot' = e11*10000/2400 - f5/20 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 7/5 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 7/50
[]  % Chaning RoadResistance to 0.5 times old value
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1/4 AND (ratio' = 1/3 OR ratio' = 1) --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1/4 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/40
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1/3 AND (ratio' = 1/3 OR ratio' = 3/7) --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1/3 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/30
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 3/7 AND (ratio' = 1/3 OR ratio' = 3/5) --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 3/7 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 3/70
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 3/5 AND (ratio' = 3/7 OR ratio' = 1) --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 3/5 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 3/50
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 1 AND (ratio' = 3/5 OR ratio' = 7/5) --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 1 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11* 10000/10
[]
INV(f7, f17, f5, e11, f7', f17', f5', e11', throttleby50') AND
ratio = 7/5 AND ratio' = 1 --> 
  f5dot' = e11*10000/2400 - f5/10 ;
  f7dot' = throttleby50 + 20 * f17 - 20 * f7 ;
  e11dot' = f17 * 1000 * 7/5 - f5 * 10000/6 ;
  f17dot' = 100*f7 - 100*f17 - e11*10000* 7/50
]
END;

control: MODULE =
BEGIN
OUTPUT ratio: REAL
LOCAL G1, G2, G3, G4, G5, G6: REAL
LOCAL G2Low, G3Low, G4Low, G5Low, G6Low: REAL
LOCAL G1High, G2High, G3High, G4High, G5High, G6High: REAL
INPUT f17, f5: REAL
LOCAL OutRPM: REAL
INITIALIZATION ratio = 1/4
DEFINITION G1 = 1/4; G2 = 1/3; G3 = 3/7; G4 = 3/5; G5 = 1; G6 = 7/5;
 G2Low = G1High; 
 G3Low = G2High; 
 G4Low = G3High; 
 G5Low = G4High; 
 G6Low = G5High; 
 G1High = G1 * 1800;
 G2High = G2 * 1800;
 G3High = G3 * 1800;
 G4High = G4 * 1800;
 G5High = G5 * 1800;
 G6High = G6 * 1800;
 OutRPM = f5 * 16
TRANSITION
[
 OutRPM' < G1High --> ratio' = G1
 []
 OutRPM' >= G2Low AND OutRPM' < G2High --> ratio' = G2
 []
 OutRPM' >= G3Low AND OutRPM' < G3High --> ratio' = G3
 []
 OutRPM' >= G4Low AND OutRPM' < G4High --> ratio' = G4
 []
 OutRPM' >= G5Low AND OutRPM' < G5High --> ratio' = G5
 []
 OutRPM' >= G6Low AND OutRPM' < G6High --> ratio' = G6
 []
 ELSE --> ratio' = 1
]
END;

system:MODULE  = plant || control ;

% Prove the correct property by running:
% bin/hsal2hasal -o -mdt 0.1 examples/drivetrain3.hsal
% sal-inf-bmc -i -d  4  -ice drivetrain3 correct
correct : THEOREM
system |- G( f5 < 112 AND f7 < 1350 AND f17 < 6700 );

% Get counter-example by running:
% sal-inf-bmc -d  4  drivetrain reach   
reach : THEOREM
system |- G( ratio <= 1/2 );
END
